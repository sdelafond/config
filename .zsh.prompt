local terminalColors=$(echotc Co)

if [[ $? == 0 ]] && [[ $terminalColors == 256 ]] ; then
  usernameColor=237
  hostColor=white
else # default for older terminals
  usernameColor=grey
  hostColor=cyan
fi

PROMPT=$'${PSEXTRA}%F{green}%80<...<%~%f%(1j.%F{cyan} %j&%f.) %F{33}${elapsed_str}%f%(?..%B%F{red} %??%b%f) # '
RPROMPT=$'[%B%F{'${usernameColor}'}%n%f%b %B%U%F{'${hostColor}'}'${hostnicks[$HOST_SHORT]:-$HOST_SHORT}$'%f%u%b]'

case $TERM in
  rxvt*|xterm*)
    TITLE_ESCAPE_BEGIN="\e]0;"
    TITLE_ESCAPE_END="\a" ;;
  sun*|vt*|screen*|tmux*|linux)
    TITLE_ESCAPE_BEGIN="\ek"
    TITLE_ESCAPE_END="\e\\" ;;
esac

getTmuxWindowIndex() {
  local windowShortcuts=(0 1 2 3 4 5 6 7 8 9 j k l a b)
  let index="$(tmux display-message -p '#{window_index}') + 1"
  echo $windowShortcuts[$index]
}

_setTerminalTitle() {
  print -Pn "${TITLE_ESCAPE_BEGIN}${1}${TITLE_ESCAPE_END}"
}

setTerminalTitle() {
#  setopt localoptions xtrace
  case $TERM in
    rxvt*|xterm*|screen*|tmux*)
      local cmd="$1"
      if env | G -q TMUX= ; then
	terminalTitle="$(getTmuxWindowIndex):"
      else
	terminalTitle="_:"
      fi
      if [[ $HOST_SHORT != $(hostname -s) ]] || [[ -z "$STY$TMUX" ]] ; then
        terminalTitle="$terminalTitle@${hostnicks[$HOST_SHORT]:-$HOST_SHORT}"
      else
        if [[ -n "${vcs_info_msg_1_}" ]] ; then
          terminalTitle="${terminalTitle}${vcs_info_msg_1_}"
	fi
        if [[ -n "${cmd}" ]] ; then
          terminalTitle="${terminalTitle}|${cmd}"
        fi
      fi
      if [[ $TERM != (screen|tmux)* ]] ; then
        terminalTitle="${TERM_TITLE_PREFIX}${hostnicks[$HOST_SHORT]:-$HOST_SHORT}${TERM_TITLE_SUFFIX}"
      fi

      [[ -z $terminalTitle ]] && terminalTitle=" "

      _setTerminalTitle "$terminalTitle"
  esac
}

precmd() {
  # REPORTTIME=10
  local elapsed=$(( SECONDS - cmd_start_ts ))
  elapsed_str=$(printf "%02d:%02d" $(( ${elapsed} / 60 )) $(( ${elapsed} % 60 )))

  # for the next line, check out the *vcs stuff in .zshrc
  vcs_stuff 2> /dev/null
  # those 2 are only set once, by sc(), to add the tmux/screen session
  # name; when detaching, they need to be reset
  unset TERM_TITLE_PREFIX
  unset TERM_TITLE_SUFFIX
  # just strip previous title from cmd marker
  unset TERM_TITLE_PREFIX
  unset TERM_TITLE_SUFFIX
  terminalTitle=${terminalTitle/\|*}
  _setTerminalTitle "$terminalTitle"
}

[[ "$EUID" != 0 ]] || RPROMPT="%S${RPROMPT}%s"

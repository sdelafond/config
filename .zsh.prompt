local terminalColors=$(echotc Co)

if [[ $? == 0 ]] && [[ $terminalColors == 256 ]] ; then
  usernameColor=237
  hostColor=white
else # default for older terminals
  usernameColor=grey
  hostColor=cyan
fi

PROMPT=$'${PSEXTRA}%F{green}%80<...<%~%f%(1j.%F{cyan} %j&%f.)%(?..%B%F{red} %??%b%f) # '
RPROMPT=$'[%B%F{'${usernameColor}'}%n%f%b %B%U%F{'${hostColor}'}'${hostnicks[$HOST_SHORT]:-$HOST_SHORT}$'%f%u%b]'

case $TERM in
  rxvt*|xterm*)
    TITLE_ESCAPE_BEGIN="\e]0;"
    TITLE_ESCAPE_END="\a" ;;
  sun*|vt*|screen*|tmux*|linux)
    TITLE_ESCAPE_BEGIN="\ek"
    TITLE_ESCAPE_END="\e\\" ;;
esac

getTmuxWindowIndex() {
  local windowShortcuts=(0 1 2 3 4 5 6 7 8 9 j k l a b)
  let index="$(tmux display-message -p '#{window_index}') + 1"
  echo $windowShortcuts[$index]
}

setTerminalTitle() {
#  setopt localoptions xtrace
  case $TERM in
    rxvt*|xterm*|screen*|tmux*)
      local cmd="$1"
      local title="$(getTmuxWindowIndex):"
      if [[ $HOST_SHORT != $(hostname -s) ]] || [[ -z "$STY$TMUX" ]] ; then
        title="$title@${hostnicks[$HOST_SHORT]:-$HOST_SHORT}"
      else
        if [[ -n "${vcs_info_msg_1_}" ]] ; then
            title="${title}${vcs_info_msg_1_}"
	fi
        if [[ -n "${cmd}" ]] ; then
            title="${title}|${cmd}"
        fi
      fi
      if [[ $TERM != (screen|tmux)* ]] ; then
        title="${hostnicks[$HOST_SHORT]:-$HOST_SHORT}"
      fi

      [[ -z $title ]] && title=" "

      print -Pn "${TITLE_ESCAPE_BEGIN}${title}${TITLE_ESCAPE_END}"
  esac
}

precmd() {
  # for the next line, check out the *vcs stuff in .zshrc
  vcs_stuff 2> /dev/null
  setTerminalTitle
}

postcmd() {
#  vcs_stuff 2> /dev/null
  setTerminalTitle
}

[[ "$EUID" != 0 ]] || RPROMPT="%S${RPROMPT}%s"
